FOR brillo IN Brillo
    LET isrebright = (
        FOR rebrillo IN Rebrillo
            FILTER rebrillo._from == CONCAT('User/', @username) && rebrillo._to == brillo._id
            RETURN {isrebright: true, date: DECODE_REV(rebrillo._rev).date}
    )[0]
    FILTER brillo.author == CONCAT('User/', @username) || isrebright.isrebright
    LET date = DATE_TIMESTAMP(isrebright.date) || brillo.created_at
    SORT date DESC
    LIMIT @offset, @limit
    LET rebrillos = (
        FOR c IN Rebrillo
            FILTER c._to == brillo._id
            COLLECT WITH COUNT INTO comments
            RETURN comments
    )[0]
    LET interactions = (
        FOR c IN Interactions
            FILTER c._to == brillo._id
            COLLECT WITH COUNT INTO comments
            RETURN comments
    )[0]
    LET comments = (
        FOR brilloC IN Brillo
            FILTER brilloC.comments == brillo._id
            COLLECT WITH COUNT INTO comments
            RETURN comments
    )[0]
    LET u = DOCUMENT(brillo.author)
    RETURN MERGE(brillo, { userKey: u._key, username: u.username, name: u.name, proflie_img: u.proflie_img, comments, interactions, rebrillos, rebrightedBy: isrebright.isrebright && u })
